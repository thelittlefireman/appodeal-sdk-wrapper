apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'

ext {
    artifactVersion = '2.4.9.3'
    aarMobVistaVersion = '9.0.0'
}

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 28
        versionCode 1
        versionName artifactVersion
        consumerProguardFiles 'proguard-rules.pro'
        multiDexEnabled true
    }
    defaultPublishConfig "normalRelease"
    flavorDimensions "default"
    productFlavors {
        normal { dimension "default" }
        optional { dimension "default" }
    }
}

allprojects {
    repositories {
        jcenter { url "http://jcenter.bintray.com/" }
        google()
        maven {
            url 'https://maven.google.com/'
        }
        jcenter()
        flatDir {
            dirs 'libs-aar'
        }
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.javaDirectories
}

artifacts {
    archives sourcesJar
}

dependencies {

    implementation "com.android.support:multidex:1.0.3"
    implementation "com.android.support:recyclerview-v7:28.0.0"
    implementation "com.android.support:support-v4:28.0.0"

    //Libs folder
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    optionalImplementation fileTree(dir: 'libs-ops', include: ['*.jar'])

    // --- Native libs ---
    implementation 'com.google.android.gms:play-services-ads:17.1.2'
    implementation 'com.google.android.gms:play-services-location:16.0.0'

    // --- AAR ---
    implementation 'com.squareup.picasso:picasso:2.5.2' //for Inmobi
    implementation name: 'adcolony-3.3.4', ext: 'aar'
    implementation name: "mobvista-$aarMobVistaVersion-alphab", ext: 'aar'
    implementation name: "mobvista-$aarMobVistaVersion-common", ext: 'aar'
    implementation name: "mobvista-$aarMobVistaVersion-interstitial", ext: 'aar'
    implementation name: "mobvista-$aarMobVistaVersion-interstitialvideo", ext: 'aar'
    implementation name: "mobvista-$aarMobVistaVersion-mtgdownloads", ext: 'aar'
    implementation name: "mobvista-$aarMobVistaVersion-mtgjscommon", ext: 'aar'
    implementation name: "mobvista-$aarMobVistaVersion-mtgnative", ext: 'aar'
    implementation name: "mobvista-$aarMobVistaVersion-nativeex", ext: 'aar'
    implementation name: "mobvista-$aarMobVistaVersion-playercommon", ext: 'aar'
    implementation name: "mobvista-$aarMobVistaVersion-reward", ext: 'aar'
    implementation name: "mobvista-$aarMobVistaVersion-videocommon", ext: 'aar'
    implementation name: "mobvista-$aarMobVistaVersion-videojs", ext: 'aar'
    implementation name: 'ogury-3.0.13', ext: 'aar'

}

group='com.github.thelittlefireman'
version = artifactVersion

if (android.productFlavors.size() > 0) {
    android.libraryVariants.all { variant ->
        if (variant.name.toLowerCase().contains("debug")) {
            return
        }

        def bundleTask = tasks["bundle${variant.name.capitalize()}Aar"]

        artifacts {
            archives(bundleTask.archivePath) {
                classifier variant.flavorName
                builtBy bundleTask
                name = project.name
            }
        }
    }
}